using gold.**
// -----------------------
// Silvia Takahashi
// UNIVERSIDAD DE LOS ANDES
//------------------------------
// 
//  
//  A finite state transducer that  decodes strings that were coded as follows:
//    Input:   xyz:W'#
//    where  x y and z are distinct
//        xyx in Aˆ3
//     W in A*
// Output:  xyz:X#
//  where X is obtained by replacing $ for all occurrences of xyz
// It checks to make sure that xyz does not appear in X.
//-----------------------
//   By default  the alphabet is A  = {'a', 't', 'c'}, it can be changed invoking the program with a string  of symbols. 
//   A  will contain all the the symbols in the parameter
//  
//  

var  Alph 

procedure main(args:String[]) begin
 	    
 	  
  
	if  args.length=0  then  Alph:= { 'a', 'c', 't'}
	else	
		Alph := {args[0].charAt(i) |  0 ≤ i < args[0].length()}\{'$','#'}
    end
	
	

	deCoder := createTransducer(Alph)
	print "Input strings of the form xyz:W#. with x, y, over "+Alph +"and W  over   "+ Alph+"* (end with an empty string)"
			
	
	Utilities.test(deCoder)
end




 

function createTransducer(A):ITransducer begin
	Ls := A
	M0 := {""}
	M1 := {""+a|a∈Ls}
	M2 := {""+a+b| a∈Ls,b∈Ls\{a}}
	M3 := {""+a+b+c|a∈Ls,b∈Ls\{a},c∈Ls\{b,a}}
	Q:=  {⟨s⟩|s∈M1∪ M2∪M3∪M0} ∪ {⟨s,d⟩|s∈M3, 0 ≤d≤ 2}∪{"E","F"}
	print Q
	
	Σ:= A ∪  { '#',':'}
	O := Σ  ∪  { '$'}
	q₀:=⟨""⟩
	F:={"F"} 
	return GDeterministicTransducer(Q,O,Σ,q₀,F,δ1,g,h1)
end
g(x):= ""



 

 
 function δ1(q,σ) begin
    var s:String
	
	if q = "E" or q ="F" then
		return "E"
    elseif |q| = 1 then
      s:= q[0]
   	  if s.length()  = 0  and σ≠ '#' and σ≠ ':'   and σ≠ '$' then
        return   	⟨""+σ⟩
      elseif s.length()  = 1 and σ≠s.charAt(0)   and σ≠ '#' and σ≠ ':' and σ≠ '$' then
        return   	⟨""+s+σ⟩
      elseif s.length()  = 2 and σ≠s.charAt(0) and σ≠s.charAt(1) and σ≠ '#'  and σ≠ ':' and σ≠ '$'  then
        return   	⟨""+s+σ⟩
      elseif s.length() = 3  and σ = ':' then
   	      return   	⟨s,0⟩
   	   else 
   	    	return "E"
   	   end
   	else // |q| = 2
   	  s,p:= q[0],q[1]
   	  if  σ = ':' then return   	"E" 
   	  elseif  σ = '#' then return   	"F"
   	  elseif  σ = '$' then return   	⟨s,0⟩
   	  
   	  elseif p=0 and σ=s.charAt(0) then return ⟨s,1⟩
   	  elseif p=0  then return ⟨s,0⟩
   	  elseif p=1 and  σ=s.charAt(0) then return ⟨s,1⟩
   	  elseif p=1 and  σ=s.charAt(1) then return ⟨s,2⟩
   	  elseif p=1  then return ⟨s,0⟩
   	  elseif p=2 and  σ=s.charAt(0) then return ⟨s,1⟩
   	  elseif p=2 and  σ=s.charAt(2) then return "E"
   	
   	 else return	⟨s,0⟩
   	  end
end
end
 
function h1(q,σ) begin
    var s:String
	if q = "E" or q ="F" then
		return ""
    elseif  |q| = 2 then
   	  s,p:= q[0],q[1]
   	  if  σ='#' then return  "#"
   	  elseif  σ='$' then return s
   	  elseif p=2 and  σ=s.charAt(2) then return ""
   	  else return  σ
   	  end
   	 elseif σ='$' then  return ""
   	 else return σ
end
end
